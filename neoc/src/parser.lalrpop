use std::str::FromStr;
use crate::{
    ast::{Ast, Stmt, Expr, BinaryOpType},
    CompilationError,
};
use lalrpop_util::ErrorRecovery;

grammar<'err>(errors: &'err mut Vec<ErrorRecovery<usize, Token<'input>, CompilationError>>);

extern {
    type Error = CompilationError;
}

//=========================================
// MACROS
//=========================================

CommaList<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None=> v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

//=========================================
// RULES
//=========================================

pub Program: Ast = {
    Stmt* => Ast::new(<>),
};

Stmt: Box<Stmt> = {
    <Expr> ";" => Box::new(Stmt::Expr(<>)),
    "print" <Expr> ";" => Box::new(Stmt::Print(<>)),
    ";" => Box::new(Stmt::Nop),
};

Expr = {
    <e:Expr> "+" <t:Term> => Box::new(Expr::BinaryOp(BinaryOpType::Add, e, t)),
    <e:Expr> "-" <t:Term> => Box::new(Expr::BinaryOp(BinaryOpType::Sub, e, t)),
    Term
};

Term = {
    <t:Term> "*" <f:Factor> => Box::new(Expr::BinaryOp(BinaryOpType::Mul, t, f)),
    <t:Term> "/" <f:Factor> => Box::new(Expr::BinaryOp(BinaryOpType::Div, t, f)),
    Factor
};

Factor: Box<Expr> = {
    Int32 => Box::new(Expr::Int32(<>)),
    "(" <Expr> ")",
    ! => { errors.push(<>); Box::new(Expr::Error) },
};


//=========================================
// TOKENS
//=========================================

Int32: i32 = {
    r"[0-9]+" => i32::from_str(<>).unwrap()
};
